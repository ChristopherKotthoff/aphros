#!/bin/awk -f

{
    sub(/^\.\//, "")
    f = $0
    line = 0
    while (getline < f > 0) {
	line++
	if (sub(/^[ \t]*#[ \t]*include[ \t]*"/, "")) {
	    sub(/".*/, "")
	    if (filep($0))
		print f, $0
	    else if (filep($0 = dir(f) "/" $0))
		print f, $0
	    else {
		printf("%s:%d: error: no such file '%s'\n", f, line, $0) | "cat >&2"
		exit(1)
	    }
	}
    }
}

function filep(path, aux, rc) {
  rc = getline aux < path
  close(path)
  return rc >= 0
}

function dir(path) {
    return sub(/\/[^/]*$/, "", path) ? path : "."
}
