if (USE_BACKEND_LOCAL)
  set(T "local")
  aphros_add_object(T ${T} local.cpp)
  target_link_libraries(${T} distr use_dims)
endif()

if (USE_BACKEND_CUBISM)
  set(T "cubismnc")
  aphros_add_object(T ${T} cubismnc.cpp)
  target_link_libraries(${T} hdf distr)
  target_compile_definitions(${T} PRIVATE _ALIGNBYTES_=16)
  target_compile_definitions(${T} PRIVATE _USE_HDF_)
  target_compile_options(${T} PRIVATE -Wall -Wextra -pedantic -Wno-shadow)
  if (USE_FPZIP)
    target_link_libraries(${T} fpzip)
  endif ()
  target_compile_definitions(${T} PRIVATE _USE_FPZIP_=$<BOOL:${USE_FPZIP}>)
endif()

if (USE_BACKEND_NATIVE)
  set(T "native")
  aphros_add_object(T ${T} native.cpp)
  target_link_libraries(${T} PUBLIC distr dump_raw dump_xmf PRIVATE use_dims)

  set(T "comm_manager")
  aphros_add_object(T ${T} comm_manager.cpp)
  target_link_libraries(${T} PUBLIC distr PRIVATE use_mpi)
endif()

set(T "report")
aphros_add_object(T ${T} report.cpp)
target_link_libraries(${T})

set(T "distr")
aphros_add_object(T ${T} distr.cpp)
target_link_libraries(${T}
    sysinfo report parser suspender vars histogram dumper git subcomm openmp
    dump_xmf dump_raw use_dims)

set(T "distrsolver")
aphros_add_object(T ${T} distrsolver.cpp)
target_link_libraries(${T} PUBLIC distr utildistr utilmpi argparse PRIVATE use_backend)
if (USE_BACKEND_CUBISM)
  target_link_libraries(${T} PUBLIC cubismnc)
endif()
if (USE_BACKEND_LOCAL)
  target_link_libraries(${T} PUBLIC local)
endif()
if (USE_BACKEND_NATIVE)
  target_link_libraries(${T} PUBLIC native)
endif()

set(T "distrbasic")
aphros_add_object(T ${T} distrbasic.cpp)
target_link_libraries(${T} PUBLIC distrsolver utildistr PRIVATE use_backend)
